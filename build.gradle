plugins {
  id 'fabric-loom' version '0.10-SNAPSHOT'
  id "com.modrinth.minotaur" version "1.2.1"
}
import com.modrinth.minotaur.TaskModrinthUpload

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version + "+" + project.minecraft_version
group = project.maven_group

repositories {
  maven { url = "https://maven.nucleoid.xyz" }
  maven { url = "https://maven.gegy.dev/" }
  maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {
  minecraft "com.mojang:minecraft:${project.minecraft_version}"
  mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
  modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

  modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

  modImplementation include('xyz.nucleoid:stimuli:0.2.6+1.18')

  modCompileOnly "dev.gegy:player-roles-api:1.4.2"
  modCompileOnly "me.lucko:fabric-permissions-api:0.1-SNAPSHOT"

  modRuntime("supercoder79:databreaker:0.2.8") {
    exclude module: "fabric-loader"
  }
}

processResources {
  inputs.property "version", project.version

  filesMatching("fabric.mod.json") {
    expand "version": project.version
  }
}

tasks.withType(JavaCompile).configureEach {
  it.options.encoding = "UTF-8"
  it.options.release = 17
}

java {
  withSourcesJar()
}

jar {
  from("LICENSE") {
    rename { "${it}_${project.archivesBaseName}" }
  }
}

task publishModrinth (type: TaskModrinthUpload){
  onlyIf {
    System.getenv("MODRINTH")
  }

  token = System.getenv("MODRINTH")
  projectId = 'hsRVgp6Q'
  versionNumber = version
  changelog = System.getenv("CHANGELOG")
  // On fabric, use 'remapJar' instead of 'jar'
  uploadFile = remapJar
  addGameVersion((String) project.minecraft_version)
  addLoader('fabric')
}

remapJar {
  finalizedBy publishModrinth
}
